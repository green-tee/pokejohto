// Characters
const LOCALID_JOEY = 1
const LOCALID_GEODUDE = 2

// Variables' aliases
const PLAYER_X_COORD = VAR_TEMP_1
const PLAYER_Y_COORD = VAR_TEMP_2
const POKE_BALLS_IN_BAG = VAR_TEMP_3
const POKE_BALLS_TO_GIVE = VAR_TEMP_4

mapscripts Route33_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: Route33_OnTransition
    MAP_SCRIPT_ON_RESUME: Route33_OnResume
}

script Route33_OnTransition {
    if (var(VAR_MAP_SCENE_ROUTE33) == 1) {
        # Move Joey to where he should be at the start of tutorial on catching Pokémon.
        call(Route33_EventScript_PlaceJoeyForCatchingTutorial)
    }
}

// Move Joey closer to tall grass for the tutorial on catching Pokémon.
script(local) Route33_EventScript_PlaceJoeyForCatchingTutorial {
    setobjectxyperm(LOCALID_JOEY, 54, 15)
    setobjectmovementtype(LOCALID_JOEY, MOVEMENT_TYPE_FACE_DOWN)
}

script Route33_OnResume {
    if (flag(FLAG_SYS_SPECIAL_WILD_BATTLE)) {
        # Joey's tutorial on catching Pokémon has ended, Geodude needs to be removed.
        removeobject(LOCALID_GEODUDE)
    }
}

// This script is execute when the player interacts with Joey.
script Route33_EventScript_Joey {
    if (var(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER) <= 2) {
        msgbox(Route33_Text_Joey_BugsyIsWaiting, MSGBOX_NPC)
    } else {
        msgbox(Route33_Text_Joey_BringYourPokedexHere, MSGBOX_NPC)
    }
}

// This script is executed when the player attempts to run past Joey before the tutorial on Pokémon Center.
// It also prevents the player to run past Joey before finding the Pokédex.
script Route33_EventScript_Joey_Trigger {
    lockall
    # The same script is triggered from two different positions.
    # Depending on the position, Joey has to turn South or North to face Player.
    getplayerxy(PLAYER_X_COORD, PLAYER_Y_COORD)
    if (var(PLAYER_Y_COORD) < 13) {
        turnobject(LOCALID_JOEY, DIR_NORTH)
    } else {
        turnobject(LOCALID_JOEY, DIR_SOUTH)
    }
    applymovement(LOCALID_JOEY, Movement_ExclamationMark)
    playse(SE_PIN)
    waitmovement(LOCALID_JOEY)
    waitse
    delay(90)
    if (var(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER) <= 2) {
        msgbox(Route33_Text_Joey_BlockRoadBugsyIsWaiting, MSGBOX_DEFAULT)
    } else {
        msgbox(Route33_Text_Joey_BringYourPokedexHere, MSGBOX_DEFAULT)
    }
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_ForcePlayerToTheRight)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    turnobject(LOCALID_JOEY, DIR_WEST)
    releaseall
}

movement Movement_ExclamationMark {
    emote_exclamation_mark
}

movement Movement_ForcePlayerToTheRight {
    walk_right * 2
}

// This script starts the scene where the tutorial on catching Pokémon occurs.
script Route33_EventScript_CatchingTutorialScene {
    lockall
    fadescreenspeed(FADE_TO_BLACK, 0)
    # Since this script can be triggered from two different positions, the camera must be padded accordingly.
    call(Route33_EventScript_NormalizeCameraAndPlayer)
    fadescreenspeed(FADE_FROM_BLACK, 0)
    bufferspeciesname(STR_VAR_1, VAR_STARTER_MON)
    msgbox(Route33_Text_Joey_YouWantToGoToBugsysGym)
    closemessage
    delay(30)
    applymovement(LOCALID_JOEY, Movement_TurnToGeodude)
    waitmovement(LOCALID_JOEY)
    msgbox(Route33_Text_Joey_ThereIsAGeodude)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_TurnToGeodude)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, Movement_PadCameraToGeodude)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
    applymovement(LOCALID_JOEY, Movement_Joey_TurnToPlayer)
    waitmovement(LOCALID_JOEY)
    msgbox(Route33_Text_Joey_JustWatch)
    closemessage
    turnobject(LOCALID_JOEY, DIR_SOUTH)
    fadeoutbgm(4)
    playse(SE_SUPER_EFFECTIVE)
    msgbox(Route33_Text_Joey_Yell)
    waitse
    closemessage
    setobjectmovementtype(LOCALID_GEODUDE, MOVEMENT_TYPE_FACE_UP)
    playse(SE_PIN)
    applymovement(LOCALID_GEODUDE, Movement_Geodude_Surprise)
    waitse
    waitmovement(LOCALID_GEODUDE)
    delay(30)
    playmoncry(SPECIES_GEODUDE, CRY_MODE_NORMAL)
    waitmoncry
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, Movement_PadCameraBackFromGeodude)
    applymovement(LOCALID_GEODUDE, Movement_Geodude_Attack)
    waitmovement(LOCALID_GEODUDE)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
    # Set FLAG_SYS_SPECIAL_WILD_BATTLE in order to be able to hide Geodude after the tutorial battle without a fadescreen
    setflag(FLAG_SYS_SPECIAL_WILD_BATTLE)
    special(StartCatchingTutorialBattle)
    waitstate
    clearflag(FLAG_SYS_SPECIAL_WILD_BATTLE)
    turnobject(LOCALID_JOEY, DIR_EAST)
    delay(6)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    msgbox(Route33_Text_Joey_ThisIsHowYouCatch)
    closemessage
    msgbox(Route33_Text_CatchEasierExplanation)
    closemessage
    call(Route33_EventScript_GiveSomePokeBallsToPlayer)
    msgbox(Route33_Text_Joey_YouCanBuyPokeBalls)
    closemessage
    delay(30)
    applymovement(LOCALID_JOEY, Movement_TurnToGeodude)
    waitmovement(LOCALID_JOEY)
    delay(30)
    msgbox(Route33_Text_Joey_WeAreDoneHere)
    closemessage
    delay(30)
    applymovement(LOCALID_JOEY, Movement_Joey_TurnToPlayer)
    waitmovement(LOCALID_JOEY)
    msgbox(Route33_Text_Joey_ImGoing)
    closemessage
    call(Route33_EventScript_JoeyStartWalkAway)
    delay(20)
    msgbox(Route33_Text_Joey_Dots)
    closemessage
    applymovement(LOCALID_JOEY, Movement_WalkBackToPlayer)
    waitmovement(LOCALID_JOEY)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    call(Route33_EventScript_NicknamingStarter)
    msgbox(Route33_Text_Joey_NowWeAreReallyDone)
    closemessage
    call(Route33_EventScript_JoeyStartWalkAway)
    msgbox(Route33_Text_Joey_ComeUpWithEnjoey)
    closemessage
    applymovement(LOCALID_JOEY, Movement_FinishWalkingAway)
    waitmovement(LOCALID_JOEY)
    removeobject(LOCALID_JOEY)
    setvar(VAR_MAP_SCENE_ROUTE33, 2)
    fadedefaultbgm
    delay(60)
    releaseall
}

// This script sets Player's position and the camera to a fixed location.
// The tutorial on catching Pokémon can be triggered from two different positions.
// This script makes it so that it looks the same whatever spot the player trigggered the script from.
script(local) Route33_EventScript_NormalizeCameraAndPlayer {
    getplayerxy(PLAYER_X_COORD, PLAYER_Y_COORD)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 55, 15)
    special(SpawnCameraObject)
    if (var(PLAYER_X_COORD) < 55) {
        applymovement(OBJ_EVENT_ID_CAMERA, Movement_FixCameraDownRight)
    } else {
        applymovement(OBJ_EVENT_ID_CAMERA, Movement_FixCameraDown)
    }
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
    turnobject(LOCALID_JOEY, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
}

movement Movement_FixCameraDown {
    walk_faster_down
}

movement Movement_FixCameraDownRight {
    walk_faster_right
    walk_faster_down
}

movement Movement_TurnToGeodude {
    walk_in_place_slow_down
}

movement Movement_PadCameraToGeodude {
    walk_slower_down * 3
}

movement Movement_Joey_TurnToPlayer {
    walk_in_place_right
}

movement Movement_Geodude_Surprise {
    face_up_fast
    emote_exclamation_mark
}

movement Movement_Geodude_Attack {
    walk_faster_up * 2
}

movement Movement_PadCameraBackFromGeodude {
    walk_faster_up * 3
}

// This script counts how many vanilla Poké Balls the player has.
// Joey will say different things depending on how many the player has.
// If the player has less than 15, Joey gives some so that the player will have 20.
script(local) Route33_EventScript_GiveSomePokeBallsToPlayer {
    countitem(ITEM_POKE_BALL, POKE_BALLS_IN_BAG)
    if (var(POKE_BALLS_IN_BAG) == 0) {
        # Joey points out that Player has no Poké Balls.
        msgbox(Route33_Text_Joey_YouHaveNoPokeBalls)
        closemessage
        giveitem(ITEM_POKE_BALL, 20)
    } elif (var(POKE_BALLS_IN_BAG) < 15) {
        # Joey points out that Player has some Poké Balls.
        msgbox(Route33_Text_Joey_YouHaveSomePokeBalls)
        closemessage
        setvar(POKE_BALLS_TO_GIVE, 20)
        subvar(POKE_BALLS_TO_GIVE, POKE_BALLS_IN_BAG)
        giveitem(ITEM_POKE_BALL, POKE_BALLS_TO_GIVE)
    } else {
        # Joey will give no Poké Balls.
        msgbox(Route33_Text_Joey_YouHaveALotOfPokeBalls)
        closemessage
    }
}

// This script moves Joey to the south towards Azalea Town.
// Player faces Joey while he is walking and Joey stops after a few steps.
// This script is separate so that it can be called twice.
script(local) Route33_EventScript_JoeyStartWalkAway {
    applymovement(OBJ_EVENT_ID_PLAYER, Movement_TurnToGeodude)
    applymovement(LOCALID_JOEY, Movement_WalkAwayFake)
    waitmovement(LOCALID_JOEY)
}

movement Movement_WalkAwayFake {
    walk_down * 2
}

movement Movement_WalkBackToPlayer {
    walk_up * 2
    face_right
}

// This script handles the nicknaming of Player's starter.
// Joey asks twice if the player wants to nickname the starter.
script(local) Route33_EventScript_NicknamingStarter {
    bufferspeciesname(STR_VAR_1, VAR_STARTER_MON)
    msgbox(Route33_Text_Joey_DoYouWantToNicknameYourPokemon, MSGBOX_YESNO)
    if (var(VAR_RESULT) == NO) {
        # Ask a second and final time.
        msgbox(Route33_Text_Joey_AreYouSure, MSGBOX_YESNO)
    }
    if (var(VAR_RESULT) == YES) {
        # The player answered "Yes" the first or the second time.
        setvar(VAR_0x8004, 0)
        call(EventScript_ChangePokemonNickname)
    } else {
        # The player answered "No" both times.
        msgbox(Route33_Text_Joey_OkNevermindThen)
        closemessage
    }
}

movement Movement_FinishWalkingAway {
    walk_down
    walk_left * 7
    walk_up * 6
    walk_left
}

// This script is executed when the player reads the sign explaining how ledges work.
script Route33_EventScript_Sign_Ledges {
    msgbox(Route33_Text_TrainerTipsLedges, MSGBOX_SIGN)
}
