// Characters
const LOCALID_JOEY = 1
const LOCALID_BUGSY = 2

// Starter balls
const LOCALID_CHIKORITA_BALL = 3
const LOCALID_CYNDAQUIL_BALL = 4
const LOCALID_TOTODILE_BALL = 5

// Variables' aliases
const PLAYER_INTERACTED_STARTER = VAR_TEMP_1
const JOEY_STARTER = VAR_TEMP_2
const BUGSY_STARTER = VAR_TEMP_3
const VARID_JOEY_BALL = VAR_TEMP_4
const VARID_BUGSY_BALL = VAR_TEMP_5
const TRAINER_JOEY_FIRST_BATTLE = VAR_TEMP_6
const TRAINER_BUGSY_FIRST_BATTLE = VAR_TEMP_7


mapscripts RouteOasis_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: RouteOasis_EventScript_OnTransition
    MAP_SCRIPT_ON_WARP_INTO_MAP_TABLE [VAR_MAP_SCENE_ROUTE_OASIS, 0: RouteOasis_EventScript_AdjustPlayerOnStart]
    MAP_SCRIPT_ON_FRAME_TABLE [VAR_MAP_SCENE_ROUTE_OASIS, 0: RouteOasis_EventScript_BugsyIntro]
}

script RouteOasis_EventScript_OnTransition {
    if (var(VAR_MAP_SCENE_ROUTE_OASIS) == 1) {
        # Make sure Joey and Bugsy stay in place after control is given to the player.
        call(RouteOasis_EventScript_AdjustBugsyAndJoey)
    }
}

// Make Player face Bugsy at the start of the game, without making it visible to the player.
script RouteOasis_EventScript_AdjustPlayerOnStart {
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_EAST)
}

// Intro, initial dialogues.
// Bugsy spawns Poké Balls containing starters.
// After this script ends, control is given to the player for the first time.
script RouteOasis_EventScript_BugsyIntro {
    lockall
    call(RouteOasis_EventScript_BugsyIntroSpeech)
    call(RouteOasis_EventScript_BugsyIntroLayPokeballs)
    call(RouteOasis_EventScript_BugsyIntroExplainStarters)
    setvar(VAR_MAP_SCENE_ROUTE_OASIS, 1)
    msgbox(RouteOasis_Text_Tutorial_WalkAndRun, MSGBOX_DEFAULT)
    # This textbox should stay open until the player interacts with something.
    releaseall
}

// Dialogue before Bugsy spawns the Poké Balls.
script(local) RouteOasis_EventScript_BugsyIntroSpeech {
    msgbox(RouteOasis_Text_Bugsy_YouWantToBecomeATrainer)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_FaceJoey)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_JoeyNewGymLeader)
    closemessage
    call(RouteOasis_EventScript_JoeyExclamationEmote)
    delay(120)
    msgbox(RouteOasis_Text_Joey_BugsyDidNotNeedTo)
    closemessage
    delay(30)
    msgbox(RouteOasis_Text_Bugsy_TheseMonsAreRare)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_FacePlayer)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_LastChallenger)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, RouteOasis_Movement_Player_OutOfTheWay)
    waitmovement(OBJ_EVENT_ID_PLAYER)
}

// Makes an exclamation mark appear over Joey's head, with sound and waits.
// This script is isolated because it is called more than once.
script(local) RouteOasis_EventScript_JoeyExclamationEmote {
    applymovement(LOCALID_JOEY, RouteOasis_Movement_Surprised)
    playse(SE_PIN)
    waitmovement(LOCALID_JOEY)
    waitse
}

// Bugsy walks to the metatiles where the Poké Balls should appear and makes them visible.
// Every Poké Ball is laid down separately, with movement, sound and delay.
script(local) RouteOasis_EventScript_BugsyIntroLayPokeballs {
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_GoLayingFirstBall)
    delay(60)
    turnobject(LOCALID_JOEY, DIR_WEST)
    waitmovement(LOCALID_BUGSY)
    addobject(LOCALID_CHIKORITA_BALL)
    clearflag(FLAG_HIDE_CHIKORITA_BALL)
    playse(SE_BALL)
    waitse
    delay(30)
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_GoLayingNextBall)
    waitmovement(LOCALID_BUGSY)
    addobject(LOCALID_CYNDAQUIL_BALL)
    clearflag(FLAG_HIDE_CYNDAQUIL_BALL)
    playse(SE_BALL)
    waitse
    delay(30)
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_GoLayingNextBall)
    waitmovement(LOCALID_BUGSY)
    addobject(LOCALID_TOTODILE_BALL)
    clearflag(FLAG_HIDE_TOTODILE_BALL)
    playse(SE_BALL)
    waitse
    delay(30)
}

// Dialogue after Bugsy spawns Poké Balls.
script(local) RouteOasis_EventScript_BugsyIntroExplainStarters {
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_GoExplainStartersToPlayer)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_PlayerPickMon)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_FaceJoeyFromLeft)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_JoeyWillPickSecond)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_FacePokeballs)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_IWillPickLast)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_FacePlayerFromAbove)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_GoAheadPickOne)
    closemessage
    delay(30)
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_WaitPlayerPickStarter)
    waitmovement(LOCALID_BUGSY)
    call(RouteOasis_EventScript_AdjustBugsyAndJoey)
}

// This script changes Bugsy's position and Bugsy's and Joey's facing.
script(local) RouteOasis_EventScript_AdjustBugsyAndJoey {
    setobjectxyperm(LOCALID_BUGSY, 23, 31)
    setobjectmovementtype(LOCALID_BUGSY, MOVEMENT_TYPE_FACE_RIGHT)
    setobjectmovementtype(LOCALID_JOEY, MOVEMENT_TYPE_FACE_LEFT)
}

// This script is triggered when the player attempts to go away to the left of the Poké Balls.
// It ensures the player cannot go away without a Pokémon.
script RouteOasis_EventScript_BugsyPreventPlayerFromGoingAway {
    lockall
    turnobject(LOCALID_BUGSY, DIR_SOUTH)
    msgbox(RouteOasis_Text_Bugsy_DoNotGoAway)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, RouteOasis_Movement_Player_WalkToPokeballsFromLeft)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    turnobject(LOCALID_BUGSY, DIR_EAST)
    releaseall
}

// This script is triggered when the player attempts to go away to the right of the Poké Balls.
// It ensures the player cannot go away without a Pokémon.
script RouteOasis_EventScript_JoeyPreventPlayerFromGoingAway {
    lockall
    turnobject(LOCALID_JOEY, DIR_EAST)
    msgbox(RouteOasis_Text_Joey_DoNotGoAway)
    closemessage
    applymovement(OBJ_EVENT_ID_PLAYER, RouteOasis_Movement_Player_WalkToPokeballsFromRight)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    turnobject(LOCALID_JOEY, DIR_WEST)
    releaseall
}

movement RouteOasis_Movement_FaceJoey {
    walk_in_place_fast_up
}

movement RouteOasis_Movement_Surprised {
    emote_exclamation_mark
}

movement RouteOasis_Movement_FacePlayer {
    walk_in_place_left
}

movement RouteOasis_Movement_Player_OutOfTheWay {
    walk_down
    walk_left
    face_up
}

movement RouteOasis_Movement_GoLayingFirstBall {
    walk_left * 3
    walk_in_place_fast_up
}

movement RouteOasis_Movement_GoLayingNextBall {
    walk_right
    walk_in_place_fast_up
}

movement RouteOasis_Movement_GoExplainStartersToPlayer {
    walk_left
    walk_in_place_fast_down
}

movement RouteOasis_Movement_Bugsy_FaceJoeyFromLeft {
    walk_in_place_right
}

movement RouteOasis_Movement_FacePokeballs {
    walk_in_place_slow_up
}

movement RouteOasis_Movement_FacePlayerFromAbove {
    walk_in_place_down
}

movement RouteOasis_Movement_WaitPlayerPickStarter {
    walk_left
    walk_left
    face_right
}

movement RouteOasis_Movement_Player_WalkToPokeballsFromLeft {
    walk_right * 2
}

movement RouteOasis_Movement_Player_WalkToPokeballsFromRight {
    walk_left * 2
}

// This script is executed when the player talks to Bugsy before selecting a Pokémon.
script RouteOasis_EventScript_Bugsy {
    msgbox(RouteOasis_Text_Bugsy_CheckOutPokeballs, MSGBOX_NPC)
}

// This script is executed when the player talks to Joey before selecting a Pokémon.
script RouteOasis_EventScript_Joey {
    msgbox(RouteOasis_Text_Joey_CheckOutPokeballs, MSGBOX_NPC)
}

// This script is executed when the player interacts with one of the Poké Balls.
// Depending on the Poké Ball's ID, the player is shown a different Pokémon.
// The player is asked if they want to pick the selected Pokémon as starter.
// If not, the script ends and the player can move freely again.
// If the player confirms the Pokémon, the intro is carried on.
script RouteOasis_EventScript_StarterBall {
    lockall
    call(RouteOasis_EventScript_SetupInteractedStarterInfo)
    if (var(VAR_RESULT) == YES) {
        # A starter is given to the player and the intro is carried on.
        call(RouteOasis_EventScript_GiveStarterToPlayer)
        call(RouteOasis_EventScript_PositionCharactersAfterSelection)
        setvar(VAR_MAP_SCENE_ROUTE_OASIS, 2)
        call(RouteOasis_EventScript_JoeyStarterSelection)
        call(RouteOasis_EventScript_BugsyStarterSelection)
        call(RouteOasis_EventScript_PositionCharactersBeforeFirstBattle)
        call(RouteOasis_EventScript_BattleSequences)
        call(RouteOasis_EventScript_AfterBattleSequences)
    } else {
        # RouteOasis_EventScript_SetupInteractedStarterInfo shows a picture of the interacted Pokémon.
        # It needs to be hidden here if the Pokémon hasn't been taken.
        # Otherwise, it's hidden by RouteOasis_EventScript_PositionCharactersAfterSelection.
        hidemonpic
    }
    releaseall
}

// This script depends on the Poké Ball the player has interacted with.
// The Pokémon's cry is played, it's picture is shown.
// Text color, type and name are buffered to be displayed correctly in textboxes.
script(local) RouteOasis_EventScript_SetupInteractedStarterInfo {
    switch (var(VAR_LAST_TALKED)) {
        case LOCALID_CHIKORITA_BALL:
            setvar(PLAYER_INTERACTED_STARTER, SPECIES_CHIKORITA)
            bufferstring(STR_VAR_2, "Grass")
            bufferstring(STR_VAR_3, "{COLOR GREEN}")
        case LOCALID_CYNDAQUIL_BALL:
            setvar(PLAYER_INTERACTED_STARTER, SPECIES_CYNDAQUIL)
            bufferstring(STR_VAR_2, "Fire")
            bufferstring(STR_VAR_3, "{COLOR RED}")
        case LOCALID_TOTODILE_BALL:
            setvar(PLAYER_INTERACTED_STARTER, SPECIES_TOTODILE)
            bufferstring(STR_VAR_2, "Water")
            bufferstring(STR_VAR_3, "{COLOR BLUE}")
    }
    playmoncry(PLAYER_INTERACTED_STARTER, CRY_MODE_NORMAL)
    showmonpic(PLAYER_INTERACTED_STARTER, 10, 3)
    bufferspeciesname(STR_VAR_1, PLAYER_INTERACTED_STARTER)
    msgbox(RouteOasis_Text_Ball_WantToPickThisOne, MSGBOX_YESNO)
    waitmoncry
}

// This script gives the player their first Pokémon.
// It sets the system flag that enables the Pokémon menu.
script(local) RouteOasis_EventScript_GiveStarterToPlayer {
    givemon(PLAYER_INTERACTED_STARTER, 5)
    setflag(FLAG_SYS_POKEMON_GET)
    copyvar(VAR_STARTER_MON, PLAYER_INTERACTED_STARTER)
    msgbox(RouteOasis_Text_Ball_StarterHasBecomeCompanion, MSGBOX_DEFAULT)
    playmoncry(PLAYER_INTERACTED_STARTER, CRY_MODE_NORMAL)
    call(RouteOasis_EventScript_FanfareStarterAfterCry)
}

// This script plays the fanfare after the Pokémon has been given to the player, Joey or Bugsy.
// It is isolated because it is called more than once.
script(local) RouteOasis_EventScript_FanfareStarterAfterCry {
    waitmoncry
    delay(10)
    playfanfare(MUS_CAUGHT_INTRO)
    waitfanfare
    closemessage
}

// This script is called after the starter has been given to the player.
// The Pokémon's picture is hidden here.
// After that, the player is moved to a fixed position and the camera is centered.
// Player's and Joey's facing are changed.
script(local) RouteOasis_EventScript_PositionCharactersAfterSelection {
    fadescreenspeed(FADE_TO_BLACK, 6)
    hidemonpic
    setobjectxy(OBJ_EVENT_ID_PLAYER, 24, 32)
    setobjectxy(LOCALID_JOEY, 25, 31)
    special(SpawnCameraObject)
    switch (var(VAR_LAST_TALKED)) {
        # Adjust camera depending on Player's current position.
        # The position is known because each Pokémon is only interactable from one side.
        case LOCALID_CHIKORITA_BALL:
            applymovement(OBJ_EVENT_ID_CAMERA, RouteOasis_Movement_Camera_FromChikoritaBall)
        case LOCALID_CYNDAQUIL_BALL:
            applymovement(OBJ_EVENT_ID_CAMERA, RouteOasis_Movement_Camera_FromCyndaquilBall)
        case LOCALID_TOTODILE_BALL:
            applymovement(OBJ_EVENT_ID_CAMERA, RouteOasis_Movement_Camera_FromTotodileBall)
    }
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    turnobject(LOCALID_JOEY, DIR_NORTH)
    removeobject(VAR_LAST_TALKED)
    delay(60)
    fadescreenspeed(FADE_FROM_BLACK, 6)
}

movement RouteOasis_Movement_Camera_FromChikoritaBall {
    walk_faster_down
}

movement RouteOasis_Movement_Camera_FromCyndaquilBall {
    walk_faster_left
    walk_faster_down
}

movement RouteOasis_Movement_Camera_FromTotodileBall {
    walk_faster_left * 2
    walk_faster_down
}

// Dialogue after starter selection, before Joey chooses his.
// Joey walks to the Poké Ball and picks it up, despawning it.
script(local) RouteOasis_EventScript_JoeyStarterSelection {
    call(RouteOasis_EventScript_SetupBuffersAccordingToStarter)
    msgbox(RouteOasis_Text_Bugsy_YourTurnJoey)
    closemessage
    delay(60)
    applymovement(LOCALID_JOEY, RouteOasis_Movement_TurnToCamera)
    waitmovement(LOCALID_JOEY)
    msgbox(RouteOasis_Text_Joey_Dots)
    closemessage
    delay(120)
    msgbox(RouteOasis_Text_Joey_WhichOneShouldIPick)
    closemessage
    delay(60)
    applymovement(LOCALID_JOEY, RouteOasis_Movement_FacePokeballs)
    waitmovement(LOCALID_JOEY)
    msgbox(RouteOasis_Text_Joey_AdvantageOverPlayerOrOverBugsy)
    closemessage
    call(RouteOasis_EventScript_JoeyExclamationEmote)
    msgbox(RouteOasis_Text_Joey_IllTakeThisOne)
    closemessage
    call(RouteOasis_EventScript_JoeyWalkToPokeball)
    msgbox(RouteOasis_Text_Ball2_JoeyPicked, MSGBOX_DEFAULT)
    playmoncry(JOEY_STARTER, CRY_MODE_NORMAL)
    call(RouteOasis_EventScript_FanfareStarterAfterCry)
    removeobject(VARID_JOEY_BALL)
    delay(60)
}

// This script sets variables so that they contain IDs of the remaining Poké Balls.
// Names are buffered to be later displayed correctly in textboxes.
script(local) RouteOasis_EventScript_SetupBuffersAccordingToStarter {
    switch (var(VAR_STARTER_MON)) {
        case SPECIES_CHIKORITA:
            setvar(JOEY_STARTER, SPECIES_TOTODILE)
            setvar(VARID_JOEY_BALL, LOCALID_TOTODILE_BALL)
            setvar(BUGSY_STARTER, SPECIES_CYNDAQUIL)
            setvar(VARID_BUGSY_BALL, LOCALID_CYNDAQUIL_BALL)
        case SPECIES_CYNDAQUIL:
            setvar(JOEY_STARTER, SPECIES_CHIKORITA)
            setvar(VARID_JOEY_BALL, LOCALID_CHIKORITA_BALL)
            setvar(BUGSY_STARTER, SPECIES_TOTODILE)
            setvar(VARID_BUGSY_BALL, LOCALID_TOTODILE_BALL)
        case SPECIES_TOTODILE:
            setvar(JOEY_STARTER, SPECIES_CYNDAQUIL)
            setvar(VARID_JOEY_BALL, LOCALID_CYNDAQUIL_BALL)
            setvar(BUGSY_STARTER, SPECIES_CHIKORITA)
            setvar(VARID_BUGSY_BALL, LOCALID_CHIKORITA_BALL)
    }
    bufferspeciesname(STR_VAR_2, BUGSY_STARTER)
    bufferspeciesname(STR_VAR_3, JOEY_STARTER)
}

movement RouteOasis_Movement_TurnToCamera {
    walk_in_place_slow_down
}

// Apply the correct movement to Joey, depending on which Pokémon he's going to take.
// Joey will take the Starter which is weak against Player's.
script(local) RouteOasis_EventScript_JoeyWalkToPokeball {
    switch (var(JOEY_STARTER)) {
        case SPECIES_CHIKORITA:
            applymovement(LOCALID_JOEY, RouteOasis_Movement_Joey_RunToChikoritaBall)
        case SPECIES_CYNDAQUIL:
            applymovement(LOCALID_JOEY, RouteOasis_Movement_Joey_RunToCyndaquilBall)
        case SPECIES_TOTODILE:
            applymovement(LOCALID_JOEY, RouteOasis_Movement_Joey_RunToTotodileBall)
    }
    waitmovement(LOCALID_JOEY)
}

movement RouteOasis_Movement_Joey_RunToChikoritaBall {
    walk_fast_left
    walk_in_place_fast_up
}

movement RouteOasis_Movement_Joey_RunToCyndaquilBall {
    walk_in_place_fast_up
}

movement RouteOasis_Movement_Joey_RunToTotodileBall {
    walk_fast_right
    walk_in_place_fast_up
}

// This script handles the sequence in which Bugsy takes his Poké Ball.
script(local) RouteOasis_EventScript_BugsyStarterSelection {
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_FaceJoeyFromLeft)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_ThatLeavesOneForMe)
    closemessage
    call(RouteOasis_EventScript_BugsyWalkToPokeball)
    msgbox(RouteOasis_Text_Ball3_BugsyPicked, MSGBOX_DEFAULT)
    playmoncry(BUGSY_STARTER, CRY_MODE_NORMAL)
    call(RouteOasis_EventScript_FanfareStarterAfterCry)
    removeobject(VARID_BUGSY_BALL)
    delay(60)
}

// Make Bugsy walk to the correct Poké Ball according to which one the player picked.
// Bugsy will take the starter which is strong agains Player's.
script(local) RouteOasis_EventScript_BugsyWalkToPokeball {
    switch (var(BUGSY_STARTER)) {
        case SPECIES_CHIKORITA:
            applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_WalkToChikoritaBall)
        case SPECIES_CYNDAQUIL:
            applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_WalkToCyndaquilBall)
        case SPECIES_TOTODILE:
            applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_WalkToTotodileBall)
    }
    waitmovement(LOCALID_BUGSY)
}

movement RouteOasis_Movement_Bugsy_WalkToChikoritaBall {
    walk_up
    walk_in_place_right
}

movement RouteOasis_Movement_Bugsy_WalkToCyndaquilBall {
    walk_up
    walk_right
}

movement RouteOasis_Movement_Bugsy_WalkToTotodileBall {
    walk_up
    walk_right * 2
}

// This script positions Player, Joey and Bugsy after selection and before first battle.
// The positioning occurs while the screen is black.
script(local) RouteOasis_EventScript_PositionCharactersBeforeFirstBattle {
    fadescreenspeed(FADE_TO_BLACK, 6)
    setobjectxy(LOCALID_JOEY, 27, 30)
    setobjectxy(LOCALID_BUGSY, 25, 31)
    setobjectxy(OBJ_EVENT_ID_PLAYER, 27, 31)
    turnobject(LOCALID_JOEY, DIR_WEST)
    turnobject(LOCALID_BUGSY, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, RouteOasis_Movement_Camera_BeforeFirstBattle)
    waitmovement(OBJ_EVENT_ID_CAMERA)
    special(RemoveCameraObject)
    delay(120)
    fadescreenspeed(FADE_FROM_BLACK, 6)
}

movement RouteOasis_Movement_Camera_BeforeFirstBattle {
    walk_faster_up
    walk_faster_right * 3
}

script(local) RouteOasis_EventScript_BattleSequences {
    call(RouteOasis_EventScript_JoeyBattleSequence)
    delay(120)
    call(RouteOasis_EventScript_BugsyBattleSequence)
}

// This script carries the first battle agains Joey, along with dialogues before and after the fight.
script(local) RouteOasis_EventScript_JoeyBattleSequence {
    msgbox(RouteOasis_Text_Bugsy_OneThingLeftToDo)
    closemessage
    msgbox(RouteOasis_Text_Joey_AnswerBugsySure)
    applymovement(LOCALID_JOEY, RouteOasis_Movement_FacePlayerFromAbove)
    waitmovement(LOCALID_JOEY)
    msgbox(RouteOasis_Text_Joey_WeNeedToShowPlayerHowToBattle)
    closemessage
    delay(60)
    msgbox(RouteOasis_Text_Bugsy_FairBattlesAreGood)
    closemessage
    delay(60)
    applymovement(OBJ_EVENT_ID_PLAYER, RouteOasis_Movement_FacePokeballs)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(RouteOasis_Text_Joey_IllGoEasyOnYou)
    closemessage
    call(RouteOasis_EventScript_SetupBattleVariables)
    call(RouteOasis_EventScript_DoFirstJoeyBattle)
    # Joey and Bugsy will now have different lines if Player loses
    if (defeated(TRAINER_JOEY_FIRST_BATTLE)) {
        msgbox(RouteOasis_Text_Joey_AfterHeLoses)
        closemessage
        delay(60)
        msgbox(RouteOasis_Text_Bugsy_AfterJoeyLoses)
    } else {
        msgbox(RouteOasis_Text_Joey_AfterHeWins)
        closemessage
        delay(60)
        msgbox(RouteOasis_Text_Bugsy_AfterJoeyWins)
    }
    closemessage
}

// This script starts the first battle against Joey with the correct Pokémon.
// Joey has the one that is weak against Player's.
script(local) RouteOasis_EventScript_DoFirstJoeyBattle {
    switch(var(VAR_STARTER_MON)) {
        case SPECIES_CHIKORITA:
            trainerbattle_earlyrival(TRAINER_JOEY_FIRST_BATTLE_TOTODILE, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Joey_TextWhenHeLoses, RouteOasis_Text_Joey_TextWhenHeWins)
        case SPECIES_CYNDAQUIL:
            trainerbattle_earlyrival(TRAINER_JOEY_FIRST_BATTLE_CHIKORITA, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Joey_TextWhenHeLoses, RouteOasis_Text_Joey_TextWhenHeWins)
        case SPECIES_TOTODILE:
            trainerbattle_earlyrival(TRAINER_JOEY_FIRST_BATTLE_CYNDAQUIL, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Joey_TextWhenHeLoses, RouteOasis_Text_Joey_TextWhenHeWins)
    }
}

// This script sets temporary variables to know which battle took place agains the right trainer.
// The player will fight Joey, who has the weaker starter.
// Then the player will fight Bugsy, who has the stronger starter.
script(local) RouteOasis_EventScript_SetupBattleVariables {
    switch (var(VAR_STARTER_MON)) {
        case SPECIES_CHIKORITA:
            setvar(TRAINER_JOEY_FIRST_BATTLE, TRAINER_JOEY_FIRST_BATTLE_TOTODILE)
            setvar(TRAINER_BUGSY_FIRST_BATTLE, TRAINER_BUGSY_FIRST_BATTLE_CYNDAQUIL)
        case SPECIES_CYNDAQUIL:
            setvar(TRAINER_JOEY_FIRST_BATTLE, TRAINER_JOEY_FIRST_BATTLE_CHIKORITA)
            setvar(TRAINER_BUGSY_FIRST_BATTLE, TRAINER_BUGSY_FIRST_BATTLE_TOTODILE)
        case SPECIES_TOTODILE:
            setvar(TRAINER_JOEY_FIRST_BATTLE, TRAINER_JOEY_FIRST_BATTLE_CYNDAQUIL)
            setvar(TRAINER_BUGSY_FIRST_BATTLE, TRAINER_BUGSY_FIRST_BATTLE_CHIKORITA)
    }
}

// This script carries the first battle against Bugsy, along with dialogues before and after the fight.
script(local) RouteOasis_EventScript_BugsyBattleSequence {
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_WalkToHealPlayer)
    waitmovement(LOCALID_BUGSY)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    delay(30)
    msgbox(RouteOasis_Text_Bugsy_IHaveRemedies)
    closemessage
    call(RouteOasis_EventScript_HealWaitFanfareNoFadescreen)
    msgbox(RouteOasis_Text_HealExplanation, MSGBOX_DEFAULT)
    closemessage
    delay(60)
    msgbox(RouteOasis_Text_Bugsy_MyTurn)
    closemessage
    call(RouteOasis_EventScript_DoFirstBugsyBattle)
    # Bugsy will have different lines if Player lost
    if (defeated(TRAINER_BUGSY_FIRST_BATTLE)) {
        msgbox(RouteOasis_Text_Bugsy_AfterHeLoses)
    } else {
        msgbox(RouteOasis_Text_Bugsy_AfterHeWins)
    }
    closemessage
    delay(60)
}

// This script heals the Player's party and waits for the healing fanfare to finish.
script(local) RouteOasis_EventScript_HealWaitFanfareNoFadescreen {
    special(HealPlayerParty)
    call(RouteOasis_EventScript_PlayAndWaitHealingFanfare)
}

// This script plays the healing fanfare and waits for it to finish.
script(local) RouteOasis_EventScript_PlayAndWaitHealingFanfare {
    playfanfare(MUS_HEAL)
    waitfanfare
}

movement RouteOasis_Movement_Bugsy_WalkToHealPlayer {
    walk_right
}

// This script starts the first battle against Bugsy with the correct Pokémon.
// Bugsy has the one that is strong against Player's.
script(local) RouteOasis_EventScript_DoFirstBugsyBattle {
    switch(var(VAR_STARTER_MON)) {
        case SPECIES_CHIKORITA:
            trainerbattle_earlyrival(TRAINER_BUGSY_FIRST_BATTLE_CYNDAQUIL, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Bugsy_TextWhenHeLoses, RouteOasis_Text_Bugsy_TextWhenHeWins)
        case SPECIES_CYNDAQUIL:
            trainerbattle_earlyrival(TRAINER_BUGSY_FIRST_BATTLE_TOTODILE, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Bugsy_TextWhenHeLoses, RouteOasis_Text_Bugsy_TextWhenHeWins)
        case SPECIES_TOTODILE:
            trainerbattle_earlyrival(TRAINER_BUGSY_FIRST_BATTLE_CHIKORITA, RIVAL_BATTLE_TUTORIAL, RouteOasis_Text_Bugsy_TextWhenHeLoses, RouteOasis_Text_Bugsy_TextWhenHeWins)
    }
}

// This script handles the scene after Bugsy has been battled.
// Bugsy and Joey are removed from the scene.
script(local) RouteOasis_EventScript_AfterBattleSequences {
    msgbox(RouteOasis_Text_Bugsy_LetMeHealAgain)
    closemessage
    call(RouteOasis_EventScript_HealWaitFanfareNoFadescreen)
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_WalkToHealJoey)
    waitmovement(LOCALID_BUGSY)
    turnobject(LOCALID_JOEY, DIR_WEST)
    msgbox(RouteOasis_Text_Bugsy_HealJoeysPokemon)
    closemessage
    call(RouteOasis_EventScript_PlayAndWaitHealingFanfare)
    msgbox(RouteOasis_Text_Joey_ThanksBugsy)
    closemessage
    turnobject(LOCALID_BUGSY, DIR_SOUTH)
    turnobject(LOCALID_JOEY, DIR_SOUTH)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_NORTH)
    msgbox(RouteOasis_Text_Bugsy_YourFirstTask)
    closemessage
    delay(60)
    msgbox(RouteOasis_Text_Bugsy_GoToThePokemonCenter)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_FaceJoeyFromLeft)
    waitmovement(LOCALID_BUGSY)
    msgbox(RouteOasis_Text_Bugsy_ImGoing)
    closemessage
    applymovement(LOCALID_BUGSY, RouteOasis_Movement_Bugsy_RunAwayToPokeCenter)
    waitmovement(LOCALID_BUGSY)
    removeobject(LOCALID_BUGSY)
    delay(90)
    applymovement(LOCALID_JOEY, RouteOasis_Movement_FacePlayerFromAbove)
    waitmovement(LOCALID_JOEY)
    msgbox(RouteOasis_Text_Joey_ImGoingToo)
    closemessage
    applymovement(LOCALID_JOEY, RouteOasis_Movement_Joey_WalkAwayToSandalineColony)
    waitmovement(LOCALID_JOEY)
    removeobject(LOCALID_JOEY)
    delay(120)
}

movement RouteOasis_Movement_Bugsy_WalkToHealJoey {
    walk_up
    face_right
}

movement RouteOasis_Movement_Bugsy_RunAwayToPokeCenter {
    walk_fast_down * 2
    walk_fast_right * 5
    walk_fast_down * 7
}

movement RouteOasis_Movement_Joey_WalkAwayToSandalineColony {
    walk_left
    walk_down * 2
    walk_right * 5
    walk_down * 7
}
