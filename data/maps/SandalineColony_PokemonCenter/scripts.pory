// Characters
const LOCALID_BUGSY = 5

// Variables' alieases
const PLAYER_X_COORD = VAR_TEMP_1
const PLAYER_Y_COORD = VAR_TEMP_2

mapscripts SandalineColony_PokemonCenter_MapScripts {
    MAP_SCRIPT_ON_TRANSITION: SandalineColony_PokemonCenter_EventScript_OnTransition
    MAP_SCRIPT_ON_RESUME: CableClub_OnResume
    MAP_SCRIPT_ON_FRAME_TABLE [VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER, 1: SandalineColony_PokemonCenter_EventScript_BugsyStartPokeCenterTour]
}

script SandalineColony_PokemonCenter_EventScript_OnTransition {
    setrespawn(SPAWN_SANDALINE_COLONY)
    if (var(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER) == 2) {
        # Make sure Bugsy stays in place after control is given to the player.
        call(SandalineColony_PokemonCenter_EventScript_MoveBugsyToCounter)
    }
}

// Move Bugsy to the counter so that he stays there until the player has interacted with the nurse.
script(local) SandalineColony_PokemonCenter_EventScript_MoveBugsyToCounter {
    setobjectxyperm(LOCALID_BUGSY, 6, 4)
}

// Intro to the tutorial on the Pokémon Center.
// This script carries the tutorial before the player is asked to talk to the nurse.
script SandalineColony_PokemonCenter_EventScript_BugsyStartPokeCenterTour {
    lockall
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_WalkInPlaceToTalkToPlayer)
    waitmovement(LOCALID_BUGSY)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_ThisIsAPokemonCenter)
    closemessage
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_Bugsy_WalkToCounter)
    delay(40)
    applymovement(OBJ_EVENT_ID_PLAYER, SandalineColony_PokemonCenter_Movement_Player_WalkToCounter)
    waitmovement(LOCALID_BUGSY)
    waitmovement(OBJ_EVENT_ID_PLAYER)
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_WalkInPlaceToTalkToPlayer)
    waitmovement(LOCALID_BUGSY)
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_TheMainAttraction)
    closemessage
    setvar(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER, 2)
    releaseall
}

movement SandalineColony_PokemonCenter_Movement_WalkInPlaceToTalkToPlayer {
    walk_in_place_right
}

movement SandalineColony_PokemonCenter_Movement_Bugsy_WalkToCounter {
    walk_down
    walk_right
    walk_down * 2
    walk_right * 4
    walk_up
    face_right
}

movement SandalineColony_PokemonCenter_Movement_Player_WalkToCounter {
    walk_down * 3
    walk_right * 5
    walk_up
    face_left
}

// This script is executed when the player interacts with the nurse.
// Only after Bugsy has explained the Pokémon Center, the nurse acts like every other nurse.
script SandalineColony_PokemonCenter_EventScript_Nurse {
    lock
    faceplayer
    if (var(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER) < 2) {
        call(SandalineColony_PokemonCenter_EventScript_NurseBeforeTutorial)
    } elif (var(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER) == 2) {
        call(SandalineColony_PokemonCenter_EventScript_NurseTutorialHeal)
    } else {
        call(EventScript_PkmnCenterNurse)
    }
    release
}

// This script is executed when the player talks to the nurse before Bugsy has explained the Pokémon Center.
script(local) SandalineColony_PokemonCenter_EventScript_NurseBeforeTutorial {
    message(SandalineColony_PokemonCenter_Text_Nurse_HowMayIHelpYou)
    do {
        waitmessage
        multichoice(10, 6, MULTICHOICE_NURSE_BEFORE_TUTORIAL, TRUE)
        switch (var(VAR_RESULT)) {
            case 0:
                msgbox(SandalineColony_PokemonCenter_Text_Nurse_ThisIsAPokemonCenter)
            case 1:
                msgbox(SandalineColony_PokemonCenter_Text_Nurse_YouAreLookingForAFriend)
            case 2:
                msgbox(SandalineColony_PokemonCenter_Text_Nurse_TellMeIfYouNeed)
        }
        closemessage
        if (var(VAR_RESULT) != 2) {
            # If Player decided to quit the dialog, the nurse doesn't have to ask if there's more.
            message(SandalineColony_PokemonCenter_Text_Nurse_IsThereAnythingElse)
        }
        # Repeat the loop until the player chooses to quit the dialog.
    } while (var(VAR_RESULT) != 2)
}

// This script is executed when the player talkt to the nurse in Bugsy's tutorial.
// It behaves like the regual nurse script but with differend dialog and no cable club checks.
script(local) SandalineColony_PokemonCenter_EventScript_NurseTutorialHeal {
    msgbox(SandalineColony_PokemonCenter_Text_Nurse_DoYouWantToHeal, MSGBOX_YESNO)
    if (var(VAR_RESULT) == YES) {
        msgbox(SandalineColony_PokemonCenter_Text_Nurse_MayIHaveYourPokemon)
        closemessage
        bufferpartymonnick(STR_VAR_1, 0)
        msgbox(SandalineColony_PokemonCenter_Text_Scene_PlayerHandsTheParty, MSGBOX_DEFAULT)
        closemessage
        applymovement(VAR_LAST_TALKED, SandalineColony_PokemonCenter_Movement_TurnToHealMachine)
        waitmovement(VAR_LAST_TALKED)
        msgbox(SandalineColony_PokemonCenter_Text_Nurse_IllPutThemInThisMachine)
        closemessage
        special(HealPlayerParty)
        incrementgamestat(GAME_STAT_USED_POKECENTER)
        dofieldeffect(FLDEFF_POKECENTER_HEAL)
        waitfieldeffect(FLDEFF_POKECENTER_HEAL)
        msgbox(SandalineColony_PokemonCenter_Text_Nurse_Done)
        closemessage
        applymovement(VAR_LAST_TALKED, SandalineColony_PokemonCenter_Movement_TurnBackToPlayer)
        waitmovement(VAR_LAST_TALKED)
        msgbox(SandalineColony_PokemonCenter_Text_Nurse_ThankYourForYourPatience)
        closemessage
        msgbox(SandalineColony_PokemonCenter_Text_Scene_PlayerReceivedMonBack)
        closemessage
        applymovement(VAR_LAST_TALKED, Movement_Bow)
        waitmovement(VAR_LAST_TALKED)
        msgbox(Text_WeHopeToSeeYouAgain)
        closemessage
        # Set the variable so that the tutorial can finish.
        setvar(VAR_MAP_SCENE_SANDALINE_COLONY_POKECENTER, 3)
        call(SandalineColony_PokemonCenter_EventScript_EndPokemonCenterTutorial)
    } else {
        # The tutorial won't finish until the player has chosen the option to heal.
        msgbox(SandalineColony_PokemonCenter_Text_Nurse_WheneverYouWantToHeal)
        closemessage
    }
}

movement SandalineColony_PokemonCenter_Movement_TurnToHealMachine {
    walk_in_place_faster_left
}

movement SandalineColony_PokemonCenter_Movement_TurnBackToPlayer {
    walk_in_place_faster_down
}

// This script terminates the healing tutorial.
// After this script, Bugsy is removed from the Pokémon Center and Player is released.
script(local) SandalineColony_PokemonCenter_EventScript_EndPokemonCenterTutorial {
    turnobject(LOCALID_BUGSY, DIR_EAST)
    turnobject(OBJ_EVENT_ID_PLAYER, DIR_WEST)
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_PokemonCenterAreImportant)
    closemessage
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_LookAtDoor)
    waitmovement(LOCALID_BUGSY)
    delay(90)
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_FacePlayerSlowly)
    waitmovement(LOCALID_BUGSY)
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_HeadToAzaleaTown)
    closemessage
    applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_LeavePokeCenter)
    waitmovement(LOCALID_BUGSY)
    removeobject(LOCALID_BUGSY)
    playse(SE_EXIT)
    delay(60)
}

// This script is executed when the player interacts with Bugsy.
script SandalineColony_PokemonCenter_EventScript_Bugsy {
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_AskTheNurseToHealYourPokemon, MSGBOX_NPC)
}

// This script is triggered when the player attempts to run away during the healing tutorial.
// The goal is to force the player to choose the option to heal.
script SandalineColony_PokemonCenter_EventScript_PreventPlayerFromEscapingHealing {
    lockall
    # The same script is triggered from different positions.
    # Depending on the position, Bugsy has to turn South or East to face Player.
    getplayerxy(PLAYER_X_COORD, PLAYER_Y_COORD)
    if (var(PLAYER_X_COORD) < 8) {
        applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_TurnSouth)
    } else {
        applymovement(LOCALID_BUGSY, SandalineColony_PokemonCenter_Movement_TurnEast)
    }
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_WhereAreYouGoing)
    waitmovement(LOCALID_BUGSY)
    closemessage
    msgbox(SandalineColony_PokemonCenter_Text_Bugsy_AskTheNurseToHealYourPokemon)
    closemessage
    specialvar(VAR_RESULT, GetPlayerFacingDirection)
    # The same script is triggered from different positions.
    # Depending on the positon, Player walks in the opposite direction as the one it came from.
    switch (var(VAR_RESULT)) {
        case DIR_WEST:
            applymovement(OBJ_EVENT_ID_PLAYER, SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromLeft)
        case DIR_SOUTH:
            applymovement(OBJ_EVENT_ID_PLAYER, SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromBelow)
        case DIR_EAST:
            applymovement(OBJ_EVENT_ID_PLAYER, SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromRight)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)
    turnobject(LOCALID_BUGSY, DIR_EAST)
    releaseall
}

movement SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromLeft {
    walk_right * 2
}

movement SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromBelow {
    walk_up
}

movement SandalineColony_PokemonCenter_Movement_WalkBackToCounterFromRight {
    walk_left * 2
}

movement SandalineColony_PokemonCenter_Movement_TurnSouth {
    walk_in_place_down
}

movement SandalineColony_PokemonCenter_Movement_TurnEast {
    walk_in_place_right
}

movement SandalineColony_PokemonCenter_Movement_LookAtDoor {
    walk_in_place_slow_down
}

movement SandalineColony_PokemonCenter_Movement_FacePlayerSlowly {
    walk_in_place_slow_right
}

movement SandalineColony_PokemonCenter_Movement_LeavePokeCenter {
    walk_down * 2
    walk_right
    walk_down * 2
}
